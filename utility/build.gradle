//region Startup Plugins

apply plugin: "com.android.library"

//endregion

//region Android

android {

  //region Compile

  compileSdkVersion deps.build_versions.compile_sdk

  //endregion

  //region Default

  defaultConfig {
    minSdkVersion deps.build_versions.min_sdk
    targetSdkVersion deps.build_versions.target_sdk
  }

  //endregion

  //region Build Types

  buildTypes {
    debug {
      minifyEnabled false
      shrinkResources false
      useProguard false
    }
    release {
      minifyEnabled true
      shrinkResources false
      useProguard true
      files { rootProject.file("settings/proguard").listFiles() }.each {
        proguardFile it
      }
    }
  }

  //endregion

  //region Compile Options

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  //endregion

  //region Gradle Properties

  buildTypes.each {
    it.buildConfigField "Boolean", "ENABLE_BUG_TRACKER", EnableBugTracker
    it.buildConfigField "Boolean", "ENABLE_DEBUG_BRIDGE", EnableDebugBridge
    it.buildConfigField "Integer", "SERVER_CONNECTION_TIMEOUT", ServerConnectionTimeout
    it.buildConfigField "String", "SERVER_LOGGING_LEVEL", ServerLoggingLevel
    it.buildConfigField "Integer", "SERVER_READ_TIMEOUT", ServerReadTimeout
    it.buildConfigField "String", "SERVER_URL", ServerUrl
    it.buildConfigField "Integer", "SERVER_WRITE_TIMEOUT", ServerWriteTimeout
  }

  //endregion
}

//endregion

//region Dependencies

dependencies {

  //region Debugging

  implementation(deps.crashlytics) { transitive = true }
  implementation deps.facebook.stetho
  implementation deps.timber

  //endregion

  //region Dependency injection

  api deps.dagger.runtime
  compileOnly deps.javax

  //endregion

  //region Miscellaneous

  api deps.commons_lang

  //endregion
}

//endregion
